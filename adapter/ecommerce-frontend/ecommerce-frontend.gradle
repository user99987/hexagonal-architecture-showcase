plugins {
  id 'com.github.node-gradle.node' version '3.5.1'
}

apply plugin: 'com.github.node-gradle.node'
apply from: '../../etc/spotless/spotless-prettier.gradle'

node {
  download = true
  version = "18.14.1"
  npmVersion = "9.5.0"
  yarnVersion = ""
}

sourceSets {
  generated
  test
}

rootProject.ext.nodeModulesPath = "${projectDir.path}/node_modules"

clean {
  delete file('coverage')
  delete file('src/generated')
}

// Starting unit tests for JS - see package.json
task runUnitTests(type: NpmTask) {
  args = ['run', 'test']
}

// Starting ESLint - see package.json
task runESLintCheck(type: NpmTask) {
  args = ['run', 'lint']
}

task runNpmAudit() {
  doLast {
    println "Front-end security audit result:"
    def stdout = new ByteArrayOutputStream()
    try {
      exec {
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
          commandLine "npm.cmd", "audit", "--registry=https://registry.npmjs.org"
        } else {
          commandLine "npm", "audit", "--registry=https://registry.npmjs.org"
        }
        standardOutput = stdout
      }
    } catch (e) {
      println "${stdout}"
      throw e
    }
    println "${stdout}"
  }
}

processResources {
  exclude '**/static/ts/**'
}

// Establishing connections and proper order of tasks to prevent conflicts and incorrect results being produced
processGeneratedResources.dependsOn 'npm_run_build'
test.dependsOn runUnitTests
test.dependsOn runESLintCheck
runUnitTests.mustRunAfter 'npm_run_build'
runESLintCheck.mustRunAfter 'npm_run_build'
