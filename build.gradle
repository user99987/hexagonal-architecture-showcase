import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'idea'
	id 'org.springframework.boot' version '3.0.2' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
	id 'com.github.spotbugs' version '5.0.13' apply false
	id 'com.diffplug.spotless' version '6.15.0' apply false
	id 'org.owasp.dependencycheck' version '8.1.0' apply false
	id 'com.gorylenko.gradle-git-properties' version '2.4.1'
	id 'com.asarkar.gradle.build-time-tracker' version '4.3.0'
	id 'com.github.ben-manes.versions' version '0.46.0' apply false
}

allprojects {
	repositories {
        mavenCentral()
        google()
    }
}

idea {
	project {
		languageLevel = JavaVersion.VERSION_17
		vcs = 'Git'
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'com.github.ben-manes.versions'
	java {
        sourceCompatibility = 17
        targetCompatibility = 17
	}
	compileJava.options.encoding = 'UTF-8'
	compileTestJava.options.encoding = 'UTF-8'

	configureDependencyManagement(project)

	task sourceJar(type: Jar) {
		from sourceSets.main.allJava
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
			exceptionFormat = 'full'
		}
	}

	gradle.startParameter.continueOnFailure = true
}

def configureDependencyManagement(subproject) {
	configure(subproject) {
		apply plugin: 'io.spring.dependency-management'

		dependencyManagement {
			imports {
				mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2022.0.1'
				mavenBom SpringBootPlugin.BOM_COORDINATES
				mavenBom 'org.junit:junit-bom:5.9.2'
			}
			dependencies {
				dependency 'ch.qos.logback:logback-classic:1.4.5'
				dependency 'ch.qos.logback:logback-core:1.4.5'
				dependency 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
				dependency 'com.fasterxml.jackson.core:jackson-core:2.14.2'
				dependency 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
				dependency 'com.google.code.findbugs:annotations:3.0.1'
                dependency 'com.google.code.gson:gson:2.10.1'
                dependency 'com.google.guava:guava:31.1-jre'
                dependency 'com.icegreen:greenmail-junit5:2.0.0-alpha-3'
				dependency 'com.h2database:h2:2.1.214'
				dependency 'com.sun.activation:jakarta.activation:2.0.1'
				dependency 'com.sun.istack:istack-commons-runtime:4.1.1'
				dependency 'com.sun.mail:jakarta.mail:2.0.1'
				dependency 'com.tngtech.archunit:archunit:1.0.1'
				dependency 'jakarta.persistence:jakarta.persistence-api:3.1.0'
                dependency 'jakarta.transaction:jakarta.transaction-api:2.0.1'
                dependency 'jakarta.validation:jakarta.validation-api:3.0.2'
				dependency 'javax.cache:cache-api:1.1.1'
				dependency 'org.apache.commons:commons-lang3:3.12.0'
				dependency 'org.apache.logging.log4j:log4j-api:2.19.0'
				dependency 'org.apache.logging.log4j:log4j-to-slf4j:2.19.0'
				dependency 'org.apache.tomcat.embed:tomcat-embed-core:11.0.0-M1'
				dependency 'org.apache.tomcat.embed:tomcat-embed-jasper:11.0.0-M1'
				dependency 'org.apache.tomcat.embed:tomcat-embed-websocket:11.0.0-M1'
				dependency 'org.apache.xmlgraphics:fop-core:2.8'
				dependency 'org.apache.xmlgraphics:xmlgraphics-commons:2.8'
				dependency 'org.assertj:assertj-core:3.24.2'
				dependency 'org.ehcache:ehcache:3.10.8'
				dependency 'org.hamcrest:hamcrest:2.2'
				dependency 'org.hibernate:hibernate-core:6.1.7.Final'
				dependency 'org.liquibase:liquibase-core:4.19.0'
				dependency 'org.postgresql:postgresql:42.5.4'
				dependency 'org.springframework.boot:spring-boot-actuator-autoconfigure:3.0.2'
				dependency 'org.springframework.boot:spring-boot-actuator:3.0.2'
				dependency 'org.springframework.boot:spring-boot-autoconfigure:3.0.2'
				dependency 'org.springframework.boot:spring-boot-starter-cache:3.0.2'
				dependency 'org.springframework.boot:spring-boot-test-autoconfigure:3.0.2'
				dependency 'org.springframework.boot:spring-boot-test:3.0.2'
				dependency 'org.springframework.boot:spring-boot:3.0.2'
				dependency 'org.springframework.data:spring-data-commons:3.0.2'
				dependency 'org.springframework.data:spring-data-jpa:3.0.2'
				dependency 'org.springframework.integration:spring-integration-core:6.0.2'
				dependency 'org.springframework.security:spring-security-config:6.0.1'
				dependency 'org.springframework.security:spring-security-core:6.0.1'
				dependency 'org.springframework.security:spring-security-crypto:6.0.1'
				dependency 'org.springframework.security:spring-security-web:6.0.1'
				dependency 'org.springframework:spring-aop:6.0.5'
				dependency 'org.springframework:spring-aspects:6.0.5'
				dependency 'org.springframework:spring-beans:6.0.5'
				dependency 'org.springframework:spring-context-support:6.0.5'
				dependency 'org.springframework:spring-context:6.0.5'
				dependency 'org.springframework:spring-core:6.0.5'
				dependency 'org.springframework:spring-expression:6.0.5'
				dependency 'org.springframework:spring-jcl:6.0.5'
				dependency 'org.springframework:spring-jdbc:6.0.5'
				dependency 'org.springframework:spring-messaging:6.0.5'
				dependency 'org.springframework:spring-orm:6.0.5'
				dependency 'org.springframework:spring-test:6.0.5'
				dependency 'org.springframework:spring-tx:6.0.5'
				dependency 'org.springframework:spring-web:6.0.5'
				dependency 'org.springframework:spring-webmvc:6.0.5'
				dependency 'org.testcontainers:junit-jupiter:1.17.6'
				dependency 'org.testcontainers:postgresql:1.17.6'
				dependency 'xml-apis:xml-apis:2.0.2'
			}
		}
	}
}

ext.configureBootProject = { subproject, archivesName ->
	configure(subproject) {

		apply plugin: 'org.springframework.boot'

		group = 'com.cp'
		archivesBaseName = archivesName

		configureDependencyManagement(subproject)
	}
}

ext.configureBootTasks = { subproject ->
	configure(subproject) {

		springBoot {
			buildInfo()
		}

		dependencies {
			developmentOnly 'org.springframework.boot:spring-boot-devtools'
		}
	}
}

ext.configureTestDependencies = { subproject ->
	configure(subproject) {
		dependencies {
			testAnnotationProcessor 'org.projectlombok:lombok'
			testImplementation('org.springframework.boot:spring-boot-starter-test') {
				exclude group: 'junit', module: 'junit'
				exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
			}

			testImplementation 'org.assertj:assertj-core'
			testImplementation 'org.junit.jupiter:junit-jupiter'
			testImplementation 'org.junit.jupiter:junit-jupiter-api'
			testImplementation 'org.junit.jupiter:junit-jupiter-params'

			testImplementation 'org.mockito:mockito-core:5.1.1'
			testImplementation 'org.mockito:mockito-junit-jupiter:5.1.1'
		}
	}
}

buildTimeTracker {
	maxWidth = 100
	sort = true
	minTaskDuration = Duration.ofMillis(1)
}

wrapper {
	gradleVersion = '7.6'
	distributionType = Wrapper.DistributionType.ALL
	distributionUrl = 'https://services.gradle.org/distributions/'
}
